apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "org.nsdev.mypeeps"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

ext {
    supportVersion = "26.1.0"
    archVersion = "1.0.0"
    archVersionA = "1.0.0-alpha9-1"
    daggerVersion = "2.11"
    ankoVersion = "0.10.1"
    groupieVersion = "2.0.0-alpha2"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.18"

    // Dagger 2 and Dagger Android
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // RXJava
    implementation "io.reactivex.rxjava2:rxjava:2.1.4"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    // Architecture Components
    implementation "android.arch.lifecycle:runtime:$archVersion"
    implementation "android.arch.lifecycle:extensions:$archVersionA"
    implementation "android.arch.persistence.room:runtime:$archVersionA"
    implementation "android.arch.persistence.room:rxjava2:$archVersionA"
    kapt "android.arch.lifecycle:compiler:$archVersionA"
    kapt "android.arch.persistence.room:compiler:$archVersionA"
    testImplementation "android.arch.persistence.room:testing:$archVersionA"

    // Groupie
    implementation "com.xwray:groupie:$groupieVersion"

    // Logging
    implementation 'com.github.ajalt:timberkt:1.3.0'

    // Retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.1'
}
